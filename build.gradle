subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'

    group = 'io.puharesource.mc'
    version = ''

    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java']
            }
        }
    }

    task copyToJars(dependsOn: 'build', type: Copy) {
        from 'build/libs/'
        into "../../jars/"
    }

    configurations {
        provided
        compile.extendsFrom provided
    }

    repositories {
        mavenCentral()

        maven {
            name 'Spigot'
            url 'https://hub.spigotmc.org/nexus/content/groups/public/'
        }

        maven {
            name 'Vault'
            url 'http://nexus.theyeticave.net/content/repositories/pub_releases'
        }

        maven {
            name 'drtshock'
            url 'http://ci.drtshock.net/plugin/repository/everything/'
        }

        maven {
            name 'kitteh-repo'
            url 'http://repo.kitteh.org/content/groups/public'
        }

        maven {
            name = 'sponge'
            url = 'http://repo.spongepowered.org/maven'
        }
    }

    dependencies {
        provided group: 'org.projectlombok', name: 'lombok', version: '1.16.6'
    }

    artifacts {
        archives jar
    }

    jar {
        dependsOn configurations.runtime
        from {
            (configurations.runtime - configurations.provided).collect {
                it.isDirectory() ? it : zipTree(it)
            }
        } {
            exclude "META-INF/*.SF"
            exclude "META-INF/*.DSA"
            exclude "META-INF/*.RSA"
        }
    }

    if (!JavaVersion.current().isJava7Compatible()) {
        logger.lifecycle("WARNING! You're trying to compile using an unsupported Java version! Please use JDK 1.7 or above!")
    }
}
